
--01 DDL
CREATE TABLE Sizes
(
Id INT PRIMARY KEY IDENTITY,
[Length] INT NOT NULL CHECK([Length] BETWEEN 10 AND 25), 
RingRange DECIMAL(18,2) NOT NULL CHECK(RingRange BETWEEN 1.5 AND 7.5)
)

CREATE TABLE Tastes
(
Id INT PRIMARY KEY IDENTITY,
TasteType VARCHAR(20) NOT NULL,
TasteStrength VARCHAR(15) NOT NULL,
ImageURL NVARCHAR(100) NOT NULL
)

CREATE TABLE Brands
(
Id INT PRIMARY KEY IDENTITY,
BrandName VARCHAR(30) NOT NULL UNIQUE,
BrandDescription VARCHAR(MAX) 
)

CREATE TABLE Cigars
(
Id INT PRIMARY KEY IDENTITY,
CigarName VARCHAR(80) NOT NULL,
BrandId INT FOREIGN KEY REFERENCES Brands(Id) NOT NULL,
TastId INT FOREIGN KEY REFERENCES Tastes(Id) NOT NULL,
SizeId INT FOREIGN KEY REFERENCES Sizes(Id) NOT NULL,
PriceForSingleCigar DECIMAL(18,2) NOT NULL,
ImageURL NVARCHAR(100) NOT NULL
)

CREATE TABLE Addresses
(
Id INT PRIMARY KEY IDENTITY,
Town VARCHAR(30) NOT NULL,
Country NVARCHAR(30) NOT NULL,
Streat NVARCHAR(100) NOT NULL,
ZIP VARCHAR(20) NOT NULL
)

CREATE TABLE Clients
(
Id INT PRIMARY KEY IDENTITY,
FirstName NVARCHAR(30) NOT NULL,
LastName NVARCHAR(30) NOT NULL,
Email NVARCHAR(50) NOT NULL,
AddressId INT FOREIGN KEY REFERENCES Addresses(Id) NOT NULL
)

CREATE TABLE ClientsCigars
(
ClientId INT FOREIGN KEY REFERENCES Clients(Id) NOT NULL, 
CigarId INT FOREIGN KEY REFERENCES Cigars(Id) NOT NULL,
PRIMARY KEY(ClientId, CigarId)
)
--02. Insert

INSERT INTO Cigars(CigarName, BrandId, TastId, SizeId, PriceForSingleCigar, ImageURL)
VALUES
('COHIBA ROBUSTO',	9,	1,	5,	15.50,	'cohiba-robusto-stick_18.jpg'),
('COHIBA SIGLO I',	9,	1,	10,	410.00,	'cohiba-siglo-i-stick_12.jpg'),
('HOYO DE MONTERREY LE HOYO DU MAIRE',	14,	5,	11,	7.50,	'hoyo-du-maire-stick_17.jpg'),
('HOYO DE MONTERREY LE HOYO DE SAN JUAN',	14,	4,	15,	32.00,	'hoyo-de-san-juan-stick_20.jpg'),
('TRINIDAD COLONIALES',	2,	3,	8,	85.21,	'trinidad-coloniales-stick_30.jpg')


INSERT INTO Addresses(Town, Country, Streat, ZIP)
VALUES
('Sofia',	'Bulgaria',	'18 Bul. Vasil levski',	'1000'),
('Athens',	'Greece',	'4342 McDonald Avenue',	'10435'),
('Zagreb',	'Croatia',	'4333 Lauren Drive',	'10000')

--03. Update

UPDATE Cigars
SET PriceForSingleCigar = PriceForSingleCigar + PriceForSingleCigar * 0.20
WHERE TastId IN (SELECT Id FROM Tastes WHERE TasteType = 'Spicy')

UPDATE Brands
SET BrandDescription = 'New description'
WHERE BrandDescription IS NULL

--04. Delete
DELETE ClientsCigars
WHERE ClientId IN (SELECT c.Id FROM Clients c JOIN Addresses a ON c.AddressId = a.Id WHERE a.Country LIKE 'c%')

DELETE Clients
WHERE AddressId IN (SELECT Id FROM Addresses WHERE Country LIKE 'c%')

DELETE Addresses
WHERE Country LIKE 'c%'

--05.	Cigars by Price
SELECT CigarName, PriceForSingleCigar, ImageURL FROM Cigars
ORDER BY PriceForSingleCigar ASC, CigarName DESC

--06. Cigars by Taste
SELECT c.Id, c.CigarName, c.PriceForSingleCigar, t.TasteType, t.TasteStrength FROM Cigars c
JOIN Tastes t ON c.TastId = t.Id
WHERE TasteType IN ('Earthy', 'Woody')
ORDER BY c.PriceForSingleCigar DESC

--07. Clients without Cigars
SELECT cl.Id,  CONCAT(cl.FirstName, ' ', cl.LastName) AS ClientName, cl.Email  FROM Clients cl
LEFT JOIN ClientsCigars cc ON cl.Id = cc.ClientId
LEFT JOIN Cigars c ON cc.CigarId = c.Id
WHERE c.Id IS NULL
ORDER BY ClientName ASC

--08.	First 5 Cigars
SELECT TOP(5) c.CigarName, c.PriceForSingleCigar, c.ImageURL FROM Cigars c
JOIN Sizes s ON s.Id = c.SizeId
WHERE (s.[Length] >= 12 AND s.RingRange > 2.55)AND (c.CigarName LIKE '%ci%' OR 
 c.PriceForSingleCigar > 50) 
ORDER BY c.CigarName ASC, c.PriceForSingleCigar DESC

--09.	Clients with ZIP Codes
SELECT CONCAT(cl.FirstName, ' ', cl.LastName) AS FullName, a.Country, a.ZIP, '$'+CAST(MAX(c.PriceForSingleCigar) AS VARCHAR)
FROM Clients cl
JOIN Addresses a ON a.Id = cl.AddressId
JOIN ClientsCigars cc ON  cc.ClientId = cl.Id
JOIN Cigars c ON c.Id = cc.CigarId
WHERE ISNUMERIC(a.ZIP) = 1
GROUP BY cl.FirstName, cl.LastName, a.Country, a.ZIP
ORDER BY FullName ASC

--10.	Cigars by Size
SELECT cl.LastName, AVG(s.[Length]) AS CigarLength, CEILING(AVG(s.RingRange)) AS CiagrRingRange FROM Clients cl
JOIN ClientsCigars cc ON  cl.Id = cc.ClientId
JOIN Cigars c ON cc.CigarId = c.Id
JOIN Sizes s ON c.SizeId = s.Id
GROUP BY  cl.LastName
ORDER BY AVG(s.[Length]) DESC

--11. Client with Cigars
CREATE FUNCTION udf_ClientWithCigars(@name NVARCHAR(30)) 
RETURNS INT
AS
BEGIN
DECLARE @result INT = (SELECT ISNULL(COUNT(c.CigarName), 0) FROM Clients cl
LEFT JOIN ClientsCigars cc ON cl.Id = cc.ClientId
LEFT JOIN Cigars c ON c.Id = cc.CigarId
WHERE cl.FirstName = @name
)

RETURN @result

END
go

SELECT dbo.udf_ClientWithCigars('Betty')

--12. Search for Cigar with Specific Taste
CREATE PROC usp_SearchByTaste(@taste VARCHAR(20))
AS
BEGIN 
SELECT c.CigarName, '$' +CAST(c.PriceForSingleCigar AS VARCHAR),
t.TasteType, b.BrandName, CAST(s.[Length] AS VARCHAR) + ' cm' AS CigarLength,
CAST(s.RingRange AS VARCHAR)  + ' cm' AS CigarRingRange FROM Cigars c
JOIN Sizes s ON c.SizeId = s.Id
JOIN Tastes t ON c.TastId = t.Id
JOIN Brands b ON b.Id = c.BrandId
WHERE t.TasteType = @taste
ORDER BY CigarLength ASC, CigarRingRange DESC
END

EXEC usp_SearchByTaste 'Woody'
